import proguard.gradle.ProGuardTask

apply plugin: 'java'
apply plugin: 'idea'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

archivesBaseName = 'MobDefense'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum file: it.archivePath
    }
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'net.sf.proguard', name: 'proguard-gradle', version: '5.2.1'
    }
}


repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
    maven {
        url 'http://repo.mcstats.org/content/repositories/public/'
    }
    maven {
        url 'http://arathia.fr/maven/'
    }
}

//noinspection GroovyAssignabilityCheck
configurations {
    included
    //noinspection GroovyAssignabilityCheck
    compile.extendsFrom included
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.spigotmc:spigot:1.10-R0.1-SNAPSHOT'
    included 'org.mcstats.bukkit:metrics:R8-SNAPSHOT'
    compile 'fr.galaxyoyo.spigot:nbtapi:1.0.5'
}

task fatjar(type: Jar) {
    baseName = archivesBaseName

    from {
        //noinspection GroovyAssignabilityCheck
        configurations.included.collect {
            it.isDirectory() ? it : zipTree(it).matching { exclude { it.toString().contains('META-INF') && it.toString() != "META-INF" } }
        }
    }
    with jar
}

task proguard(type: ProGuardTask) {
    injars 'build/libs/MobDefense.jar'
    outjars 'MobDefense.jar'
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    configurations.compile.each { libraryjars it }
    printmapping 'mobdefense.map'
    keepclasseswithmembers 'public class fr.galaxyoyo.mobdefense.MobDefense {*;}'
    keepclassmembers 'public class * implements java.io.Serializable { \
                          !static !transient <fields>; \
                      }'
    keepclassmembers 'enum * { \
        public static **[] values(); \
        public static ** valueOf(java.lang.String); \
    }'
    keepclassmembers 'public enum fr.galaxyoyo.mobdefense.NMSUtils$ServerVersion { \
                          <fields>; \
                     }'
    keep 'class * extends fr.galaxyoyo.mobdefense.towers.Tower'
    keep 'class * extends fr.galaxyoyo.mobdefense.upgrades.Upgrade'
    keepclassmembers 'public class * extends org.bukkit.event.Event { \
                          org.bukkit.event.HandlerList getHandlerList();\
                      }'
    dontshrink()
    dontoptimize()
    keepattributes '*Annotation*,Signature,SourceFile,LineNumberTable'
}

proguard.dependsOn fatjar
